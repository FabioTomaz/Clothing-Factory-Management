CREATE DATABASE [GESTAO-FABRICA-VESTUARIO-LABORAL]
GO

USE [GESTAO-FABRICA-VESTUARIO-LABORAL]
GO


CREATE TABLE ZONA(
	CODPOSTAL1			INT				CHECK(CODPOSTAL1>0),
	CODPOSTAL2			INT				CHECK(CODPOSTAL2>0),
	DISTRITO			VARCHAR(20)		NOT NULL,
	LOCALIDADE			VARCHAR(30)		NOT NULL,
	PRIMARY KEY(CODPOSTAL1,CODPOSTAL2)
);

CREATE TABLE CLIENTE(
	NCLIENTE			INT				PRIMARY KEY IDENTITY(1,1),
	NIF					CHAR(9)			,
	NOME				VARCHAR(50)		NOT NULL,
	NIB					CHAR(21)		,
	EMAIL				VARCHAR(30)		NOT NULL,
	TELEMOVEL			VARCHAR(22)		UNIQUE NOT NULL,
	CODPOSTAL1			INT				NOT NULL,
	CODPOSTAL2			INT				NOT NULL,
	RUA					VARCHAR(40)		NOT NULL,
	N_PORTA				INT				NOT NULL CHECK(N_PORTA>0),
	FOREIGN KEY(CODPOSTAL1,CODPOSTAL2) REFERENCES ZONA(CODPOSTAL1, CODPOSTAL2)
);

CREATE TABLE [FABRICA-FILIAL](
	N_FILIAL			INT				PRIMARY KEY	IDENTITY(1,1),
	EMAIL				VARCHAR(50)		UNIQUE	NOT NULL,
	TELEFONE			VARCHAR(22)		UNIQUE	NOT NULL,
	FAX					VARCHAR(22)		,
	CODPOSTAL1			INT				NOT NULL,
	CODPOSTAL2			INT				NOT NULL,
	RUA					VARCHAR(40)		NOT NULL,
	N_PORTA				INT				NOT NULL CHECK(N_PORTA>0),
	FOREIGN KEY(CODPOSTAL1,CODPOSTAL2) REFERENCES ZONA(CODPOSTAL1, CODPOSTAL2)
);

/*1 - Gestor de Stock
2 - Gestor de Produção
3 - Gestor de Vendas
4 - Gestor de Recursos Humanos */
CREATE TABLE [TIPO-UTILIZADOR](
	ID					INT				PRIMARY KEY IDENTITY(1,1),
	TIPO				VARCHAR(50)		UNIQUE NOT NULL
);

CREATE TABLE UTILIZADOR(
	N_FUNCIONARIO		INT				PRIMARY KEY	IDENTITY(1,1),
	EMAIL				VARCHAR(40)		UNIQUE	NOT NULL,
	SALARIO				DECIMAL(6,2)	NOT NULL CHECK(SALARIO>=0),
	NOME				VARCHAR(50)		NOT NULL,
	PASS				VARCHAR(30)		NOT NULL,
	TELEFONE			VARCHAR(22)		UNIQUE	NOT NULL,
	N_FABRICA			INT				NOT NULL REFERENCES [FABRICA-FILIAL](N_FILIAL),
	HORA_ENTRADA		TIME			NOT NULL,
	HORA_SAIDA			TIME			NOT NULL,
	CODPOSTAL1			INT				NOT NULL,
	CODPOSTAL2			INT				NOT NULL,
	RUA					VARCHAR(40)		NOT NULL,
	N_PORTA				INT				NOT NULL CHECK(N_PORTA>0),
	IMAGEM				IMAGE			,
	FOREIGN KEY(CODPOSTAL1,CODPOSTAL2) REFERENCES ZONA(CODPOSTAL1, CODPOSTAL2),
	CHECK(HORA_ENTRADA < HORA_SAIDA)
);

CREATE TABLE [UTILIZADOR-TIPOS](
	UTILIZADOR			INT				REFERENCES UTILIZADOR(N_FUNCIONARIO),
	ID_TIPO				INT				REFERENCES [TIPO-UTILIZADOR](ID),
	PRIMARY KEY(UTILIZADOR, ID_TIPO)
);

CREATE TABLE ESTADO(
	ID					INT				PRIMARY KEY IDENTITY(1,1),
	DESCRIÇAO			VARCHAR(50)		UNIQUE NOT NULL
);

CREATE TABLE [LOCAIS-ENTREGA-ENCOMENDA](
	ID					INT				PRIMARY KEY IDENTITY(1,1),
	DESCRICAO			VARCHAR(50)		UNIQUE NOT NULL
);

CREATE TABLE ENCOMENDA(
	N_ENCOMENDA			INT 			IDENTITY(1,1) PRIMARY KEY,
	DATA_CONFIRMACAO	DATE			NOT NULL,
	DATA_ENTREGA		DATE			,
	DATA_ENTREGA_PREV	DATE			NOT NULL,
	LOCALENTREGA		INT				REFERENCES [LOCAIS-ENTREGA-ENCOMENDA](ID),
	DESCONTO			DECIMAL(5,2)	NOT NULL DEFAULT 0.00 CHECK(DESCONTO>=0 AND DESCONTO<=100),
	ESTADO				INT				REFERENCES ESTADO(ID)	NOT NULL,
	CLIENTE				INT				NOT NULL REFERENCES CLIENTE(NCLIENTE) ON DELETE CASCADE ON UPDATE CASCADE,
	N_GESTOR_VENDA		INT				NOT NULL REFERENCES  [UTILIZADOR](N_FUNCIONARIO) ON UPDATE CASCADE
);


CREATE TABLE ETIQUETA(
	N_ETIQUETA			INT				PRIMARY KEY IDENTITY(1,1),
	NORMAS				VARCHAR(100)	NOT NULL,
	PAIS_FABRICO		VARCHAR(40)		NOT NULL,
	COMPOSICAO			VARCHAR(100)	NOT NULL
);

CREATE TABLE [PRODUTO-BASE](
	REFERENCIA			INT				IDENTITY(1,1),
	NOME				VARCHAR(50)		NOT NULL,
	IVA					DECIMAL(5,2)	NOT NULL DEFAULT 23.00,
	DATA_ALTERACAO		DATE			NOT NULL,
	INSTRUCOES_PRODUCAO	VARCHAR(200)	NOT NULL,
	N_GESTOR_PROD		INT				NOT NULL REFERENCES [UTILIZADOR](N_FUNCIONARIO) ON UPDATE CASCADE,
	IMAGEM_DESENHO		IMAGE			NOT NULL,
	PRIMARY KEY(REFERENCIA)
);

CREATE TABLE [PRODUTO-PERSONALIZADO](
	REFERENCIA			INT				REFERENCES [PRODUTO-BASE](REFERENCIA),
	TAMANHO				VARCHAR(5)		,
	COR					VARCHAR(15)		,
	ID					INT				IDENTITY(1,1),
	N_ETIQUETA			INT				REFERENCES ETIQUETA(N_ETIQUETA),
	PRECO				DECIMAL(7,2)	NOT NULL CHECK(PRECO>0),
	UNIDADES_ARMAZEM	INT				NOT NULL CHECK(UNIDADES_ARMAZEM>=0) DEFAULT 0,
	PRIMARY KEY(REFERENCIA, TAMANHO, COR, ID)
);

CREATE TABLE CONTEUDO_ENCOMENDA(
	N_ENCOMENDA			INT				REFERENCES ENCOMENDA(N_ENCOMENDA),
	REFERENCIA_PRODUTO	INT				,
	TAMANHO_PRODUTO		VARCHAR(5)		,
	COR_PRODUTO			VARCHAR(15)		,
	ID_PRODUTO			INT				,
	QUANTIDADE			INT				NOT NULL CHECK(QUANTIDADE>0) DEFAULT 1,
	FOREIGN KEY(REFERENCIA_PRODUTO, TAMANHO_PRODUTO, COR_PRODUTO, ID_PRODUTO) REFERENCES [PRODUTO-PERSONALIZADO](REFERENCIA, TAMANHO, COR, ID),
	PRIMARY KEY(N_ENCOMENDA, REFERENCIA_PRODUTO, TAMANHO_PRODUTO, COR_PRODUTO, ID_PRODUTO)
);

CREATE TABLE FORNECEDOR(
	NIF					CHAR(9)			PRIMARY KEY CHECK(NIF>0),
	EMAIL				VARCHAR(50)		NOT NULL UNIQUE,
	NOME				VARCHAR(30)		NOT NULL UNIQUE,
	FAX					VARCHAR(22)		,
	TELEFONE			VARCHAR(22)		NOT NULL,
	DESIGNACAO			VARCHAR(100)	NOT NULL,
	CODPOSTAL1			INT				NOT NULL,
	CODPOSTAL2			INT				NOT NULL,
	RUA					VARCHAR(40)		NOT NULL,
	N_PORTA				INT				NOT NULL CHECK(N_PORTA>0),
	FOREIGN KEY(CODPOSTAL1,CODPOSTAL2) REFERENCES ZONA(CODPOSTAL1, CODPOSTAL2)
);

CREATE TABLE MATERIAIS_TÊXTEIS(
	REFERENCIA_FABRICA	INT				PRIMARY KEY	IDENTITY(1,1),
	REFERENCIA_FORN		INT				NOT NULL	CHECK (REFERENCIA_FORN>0),
	NIF_FORNECEDOR		CHAR(9)			NOT NULL	REFERENCES FORNECEDOR(NIF) ON DELETE CASCADE,
	COR					VARCHAR(20)		NOT NULL,
	DESIGNACAO			VARCHAR(80)		NOT NULL,
);

CREATE TABLE PANO(
	REFERENCIA_FABRICA	INT				PRIMARY KEY REFERENCES MATERIAIS_TÊXTEIS(REFERENCIA_FABRICA) ON DELETE CASCADE,
	TIPO				VARCHAR(20)		NOT NULL,
	GRAMAGEM			INT				NOT NULL,
	AREA_ARMAZEM		DECIMAL(10,2)	NOT NULL CHECK (AREA_ARMAZEM>=0)	DEFAULT 0,
	PRECO_POR_M2		DECIMAL(10,2)	NOT NULL
);

CREATE TABLE LINHA(
	REFERENCIA_FABRICA	INT				PRIMARY KEY REFERENCES MATERIAIS_TÊXTEIS(REFERENCIA_FABRICA) ON DELETE CASCADE,
	GROSSURA			INT				NOT NULL,
	COMPRIMENTO_ARMAZEM	DECIMAL(10,2)	NOT NULL DEFAULT 0.00,
	PRECO_CEM_METROS	DECIMAL(10,2)	NOT NULL
);

CREATE TABLE ACESSORIO(
	REFERENCIA_FABRICA	INT				PRIMARY KEY REFERENCES MATERIAIS_TÊXTEIS(REFERENCIA_FABRICA) ON DELETE CASCADE,
	QUANTIDADE_ARMAZEM	INT				NOT NULL DEFAULT 0,
	PRECO_UNIDADE		DECIMAL(10,2)	NOT NULL
);

CREATE TABLE FECHO(
	REFERENCIA_FABRICA	INT				PRIMARY KEY REFERENCES ACESSORIO(REFERENCIA_FABRICA) ON DELETE CASCADE,
	COMPRIMENTO			DECIMAL(10,2)	NOT NULL,
	TAMANHO_DENTE		DECIMAL(10,2)	NOT NULL
);

CREATE TABLE MOLA(
	REFERENCIA_FABRICA	INT				PRIMARY KEY REFERENCES ACESSORIO(REFERENCIA_FABRICA) ON DELETE CASCADE,
	DIAMETRO			DECIMAL(10,2)	NOT NULL
);

CREATE TABLE BOTAO(
	REFERENCIA_FABRICA	INT				PRIMARY KEY REFERENCES ACESSORIO(REFERENCIA_FABRICA) ON DELETE CASCADE,
	DIAMETRO			DECIMAL(10,2)	NOT NULL
);
CREATE TABLE ELASTICO(
	REFERENCIA_FABRICA	INT				PRIMARY KEY REFERENCES ACESSORIO(REFERENCIA_FABRICA) ON DELETE CASCADE,
	LARGURA				DECIMAL(10,2)	NOT NULL,
	COMPRIMENTO			DECIMAL(10,2)	NOT NULL
);
CREATE TABLE [FITA-VELCRO](
	REFERENCIA_FABRICA	INT				PRIMARY KEY REFERENCES ACESSORIO(REFERENCIA_FABRICA) ON DELETE CASCADE,
	LARGURA				DECIMAL(10,2)	NOT NULL,
	COMPRIMENTO			DECIMAL(10,2)	NOT NULL
);

CREATE TABLE [MATERIAIS-PRODUTO](
	REFERENCIA			INT				,
	TAMANHO				VARCHAR(5)		,
	COR					VARCHAR(15)		,
	ID					INT				,
	REFERENCIA_FABRICA	INT				REFERENCES MATERIAIS_TÊXTEIS(REFERENCIA_FABRICA) ON DELETE CASCADE,
	QUANTIDADE			INT				NOT NULL CHECK(QUANTIDADE>=0),
	FOREIGN KEY ( REFERENCIA, TAMANHO, COR, ID )	REFERENCES [PRODUTO-PERSONALIZADO]( REFERENCIA, TAMANHO, COR, ID ),
	PRIMARY KEY(REFERENCIA, TAMANHO, COR, ID, REFERENCIA_FABRICA)
);

ALTER TABLE UTILIZADOR ADD N_FUNCIONARIO_SUPER	INT	REFERENCES [UTILIZADOR](N_FUNCIONARIO);

/*
ESTADOS DE UMA ENCOMENDA POSSIVEIS
1 - Aceite
2 - Entregue
3 - Cancelada
*/
INSERT INTO ESTADO(DESCRIÇAO) VALUES('Aceite');
INSERT INTO ESTADO(DESCRIÇAO) VALUES('Entregue');
INSERT INTO ESTADO(DESCRIÇAO) VALUES('Cancelada');


/*
ENTREGAS DE ENCOMENDA POSSIVEIS
1 - Entrega ao domicilio
2 - Entrega na fabrica
*/
INSERT INTO [LOCAIS-ENTREGA-ENCOMENDA](DESCRICAO) VALUES ('Entrega ao Domicilio');
INSERT INTO [LOCAIS-ENTREGA-ENCOMENDA](DESCRICAO) VALUES ('Entrega na Fábrica');

/*
TIPOS DE UTILIZADOR DO SISTEMA
*/
INSERT INTO [TIPO-UTILIZADOR] (TIPO) VALUES
	('Gestor da Empresa'),  --1
	('Gestor de Produção'),	--2
	('Gestor de Vendas'),   --3
	('Gestor de Recursos Humanos');	--4

ALTER TABLE [FABRICA-FILIAL] ADD CHEFE INT REFERENCES UTILIZADOR(N_FUNCIONARIO);




--PARAR AQUI e fazer o insert de todos os codigos postais

DBCC CHECKIDENT ('[UTILIZADOR]', RESEED, 0);
GO

--Qualquer empresa de vestuario tem uma sede. Essa sede corresponderá á fabrica filial 1
INSERT INTO [FABRICA-FILIAL] (EMAIL, TELEFONE, FAX, CODPOSTAL1, CODPOSTAL2, RUA, N_PORTA) VALUES
	('ftextil@mail.com', 92546545, 23585588, 3865, 331, 'Rua das favelas', 15);	

/*DBCC CHECKIDENT ('[FABRICA-FILIAL]', RESEED, 0);
GO*/
--O priemiro utilizador do sistema será o chefe da empresa. O supervisor do chefe é ele mesmo uma vez que ninguem manda nele.
INSERT INTO UTILIZADOR (NOME, EMAIL, SALARIO, PASS, TELEFONE, N_FABRICA, 
HORA_ENTRADA, HORA_SAIDA, CODPOSTAL1, CODPOSTAL2, RUA, N_PORTA) VALUES
	('Fabio Silva', 'bsfmts@gmail.com', 1250, 'qwerty', 91547855, 1, '09:00:00 AM', '18:00:00 PM', 4650,308, 'Rua da Beira', 17);


UPDATE UTILIZADOR SET N_FUNCIONARIO_SUPER=1 WHERE N_FUNCIONARIO=1;
ALTER TABLE UTILIZADOR ALTER COLUMN N_FUNCIONARIO_SUPER INT NOT NULL;

--O primeiro utilizador tem todas as permições sobre o sistema
INSERT INTO [UTILIZADOR-TIPOS](UTILIZADOR, ID_TIPO) VALUES
	(1,1),
	(1,2),
	(1,3),
	(1,4);

--Defenir o chefe da sede como o funcionario 1 iserido
UPDATE [FABRICA-FILIAL] SET CHEFE = 1 WHERE N_FILIAL = 1;
--Qualquer fabrica tem um chefe/coordenador
ALTER TABLE [FABRICA-FILIAL] ALTER COLUMN CHEFE INT NOT NULL;